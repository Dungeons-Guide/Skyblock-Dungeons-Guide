/*
 *     Dungeons Guide - The most intelligent Hypixel Skyblock Dungeons Mod
 *     Copyright (C) 2021  cyoung06
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU Affero General Public License as published
 *     by the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU Affero General Public License for more details.
 *
 *     You should have received a copy of the GNU Affero General Public License
 *     along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "com.github.Skytils:ForgeGradle:6f5327"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"

version = "3.0"
group = "kr.syeyoung.dungeonsguide"
archivesBaseName = "dungeonsguide"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    mappings = "stable_22"
    makeObfSourceJar = false
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}
dependencies {
    implementation "org.jetbrains:annotations-java5:19.0.0"
    implementation "org.java-websocket:Java-WebSocket:1.5.1"
    implementation "org.json:json:20171018"
    implementation "com.twelvemonkeys.imageio:imageio-bmp:3.7.0"


    compileOnly "org.projectlombok:lombok:1.18.20"
    compileOnly files("mods/Hychat-1.12.1-BETA.jar")
    annotationProcessor "org.projectlombok:lombok:1.18.16"

    testCompileOnly "org.projectlombok:lombok:1.18.20"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.20"
}

shadowJar {

    archiveFileName = jar.archiveFileName

    relocate "org.java_websocket", "kr.syeyoung.org.java_websocket"

    dependencies {
        include(dependency("org.java-websocket:Java-WebSocket:1.5.1"))
        include(dependency("org.slf4j:slf4j-api:1.7.25"))
        include(dependency("org.json:json:20171018"))
        include(dependency("com.twelvemonkeys..*:.*"))
    }
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}